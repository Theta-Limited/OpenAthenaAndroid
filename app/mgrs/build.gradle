apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "mil.nga.mgrs"
archivesBaseName = "mgrs-android"
version = "2.2.1"

android {
    compileSdkVersion 33
    namespace 'mil.nga.mgrs'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
    }

    configurations {
        javadocDeps
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }
    }

    task javadoc(type: Javadoc, dependsOn: 'generateReleaseRFile') {
        title = "$archivesBaseName $version API"
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.javadocDeps
        options.source = "11"
        options.links "https://ngageoint.github.io/mgrs-java/docs/api/"
        options.links "https://ngageoint.github.io/grid-java/docs/api/"
        options.links "https://ngageoint.github.io/color-java/docs/api/"
        options.links "https://ngageoint.github.io/simple-features-java/docs/api/"
        options.linksOffline "https://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        options.linksOffline "https://developers.google.com/android/reference","${android.sdkDirectory}/extras/google/google_play_services/reference"
        destinationDir = file("../javadoc/")
        failOnError false
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.java.srcDirs
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        singleVariant("release")
    }

}

afterEvaluate {

    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.getJavaCompileProvider().get().classpath.files
    })

    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                artifact javadocJar
                artifact sourcesJar

                groupId = group
                artifactId = archivesBaseName
                version = version
                pom {
                    name = 'Military Grid Reference System'
                    packaging = 'aar'
                    description = 'Library providing Military Grid Reference System (MGRS) tiles'
                    url = 'https://github.com/ngageoint/mgrs-android'

                    scm {
                        url = 'git@github.com:ngageoint/mgrs-android.git'
                        connection = 'scm:git:git@github.com:ngageoint/mgrs-android.git'
                        developerConnection = 'scm:git:git@github.com:ngageoint/mgrs-android.git'
                    }

                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://github.com/ngageoint/mgrs-android/blob/master/LICENSE.txt'
                            distribution = 'repo'
                        }
                    }

                    organization {
                        name = 'National Geospatial-Intelligence Agency'
                        url = 'https://www.nga.mil'
                    }

                    developers {
                        developer {
                            id = 'newmanw'
                            name = 'Billy Newman'
                            email = 'winewman@caci.com'
                            organizationUrl = 'https://www.caci.com/bit-systems'
                            timezone = 'UTC-07'
                        }
                        developer {
                            id = 'bosborn'
                            name = 'Brian Osborn'
                            email = 'bosborn@caci.com'
                            organizationUrl = 'https://www.caci.com/bit-systems'
                            timezone = 'UTC-07'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                def user = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
                def pw = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
                credentials {
                    username = user
                    password = pw
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}

dependencies {
    api 'com.google.android.gms:play-services-maps:18.1.0'
    api 'mil.nga:mgrs:2.1.0'
    javadocDeps 'mil.nga:mgrs:2.1.0'
}
